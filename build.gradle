import groovy.json.JsonSlurper

buildscript {
	ext {
		springBootVersion  = '2.0.6.RELEASE'
        gitversionVersion  = '0.10.0'
        buildInfoVersion   = '4.8.1'
        gradleMongoVersion = '1.0.6'
	}
	repositories {
		mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        gradlePluginPortal()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.org.unbroken-dome.gradle-plugins:gradle-gitversion-plugin:${gitversionVersion}"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:${buildInfoVersion}"
        classpath "gradle.plugin.com.sourcemuse.gradle.plugin:GradleMongoPlugin:${gradleMongoVersion}"
	}
}

allprojects {
    apply plugin: 'org.unbroken-dome.gitversion'

    group = 'com.github.filipmalczak'

    ext {
        versions = new JsonSlurper().parse(project(":").file("versions.json"))
        stableVersion = versions.stable
        snapshotVersion = versions.snapshot
    }

    gitVersion {
        // we only override branch for pushes, because version is effectively important only when publishing artifacts;
        // see publishArtifactsFromCI task
        if (System.getenv("TRAVIS_EVENT_TYPE") == "push") {
            overrideBranchName = System.getenv("TRAVIS_BRANCH")
        }
        rules {
            before {
                version = "$snapshotVersion-${branchName.hashCode()}-SNAPSHOT"
                project.ext {
                    currentState = "FEATURE"
                }
            }
            onBranch("master") {
                version = stableVersion
                project.ext {
                    currentState = "RELEASE"
                }
            }
            onBranch("dev") {
                version = "$snapshotVersion-SNAPSHOT"
                project.ext {
                    currentState = "SNAPSHOT"
                }
            }
            onBranch(~/snapshot\/v(.+)/) {
                version = "${matches[1]}-SNAPSHOT"
                project.ext {
                    currentState = "SNAPSHOT"
                }
            }
        }
    }

    version = gitVersion.determineVersion()
}

String padCenter(int minSize, String left, String right){
    int padSize = minSize - left.size() - right.size();
    if (padSize < 0)
        padSize = 0;
    return left+(" "*padSize)+right
}

task echoGroupAndVersion(group: "help", description: "Figures out project group and current version and prints them") {
    doLast {
        println padCenter(80,   "Project group:            ${project.group}", " <- artifact group  ")
        println                 "Current stable version:   ${stableVersion}"
        println                 "Current snapshot version: ${snapshotVersion}"
        println                 "Project status:           ${currentState}"
        println padCenter(80,   "Project version:          ${version}",       " <- artifact version")
    }
}

//todo fix bintray

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'io.spring.dependency-management'


    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    //todo normalize dependency notation "group:project[:version" against group: group, name: project[, version: version]
    //I think that single-string is better

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
        dependencies {
            dependency group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc4'
            dependency 'uk.com.robust-it:cloning:1.9.10'

            dependency group: 'ma.glasnost.orika', name: 'orika-core', version: '1.5.2'

            dependency 'org.junit.jupiter:junit-jupiter-api:5.4.0'
            dependency group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.23.0'
            dependency 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

            dependency group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
            dependency 'org.junit.vintage:junit-vintage-engine:5.3.1' //required for Spock
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.1'

            dependency "com.github.sbrannen:spring-test-junit5:1.2.0"


            //todo figure out if swagger is useful here
//            dependency group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
//            dependency group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'

            dependency 'org.projectlombok:lombok:1.18.2'
        }
    }

    dependencies {
////    api 'org.apiguardian:apiguardian-api:1.0.0' //todo start using this

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine'

        testImplementation group: 'org.spockframework', name: 'spock-core'
        testRuntime 'org.junit.vintage:junit-vintage-engine'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'

        compileOnly('org.projectlombok:lombok')
        testCompileOnly('org.projectlombok:lombok')
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "started", "passed", "skipped", "failed"
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            xml.destination new File(buildDir, "/reports/jacoco/report.xml")
            html.enabled true
            html.destination new File(buildDir, "/reports/jacoco/html")
        }
    }

    test.doLast jacocoTestReport.&execute

    ext {
        bintrayRepo = 'maven'
        artifactoryRepo = currentState == "RELEASE" ? 'oss-release-local' : 'oss-snapshot-local'
        publishingUser = project.hasProperty("publishingUsername") ?
                project.getProperty("publishingUsername") :
                System.getenv("PUBLISHING_USERNAME")
        publishingKey = project.hasProperty("publishingApiKey") ?
                project.getProperty("publishingApiKey") :
                System.getenv("PUBLISHING_API_KEY")
        artifactoryUrl = project.hasProperty("artifactoryContextUrl") ?
                project.getProperty("artifactoryContextUrl") :
                System.getenv("ARTIFACTORY_URL")
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            VentPublication(MavenPublication) {
                from components.java
                artifact tasks.sourceJar
            }
        }
    }

    artifactory {
        contextUrl = artifactoryUrl
        publish {
            repository {
                repoKey = artifactoryRepo
                username = publishingUser
                password = publishingKey
            }
            defaults {
                publications 'VentPublication'
                publishArtifacts = true
                publishPom = true
            }
        }
        //fixme fix build numbers
        clientConfig.setIncludeEnvVars(false)
        clientConfig.info.setBuildName("vent")
        clientConfig.info.setVcsUrl("https://github.com/FilipMalczak/vent.git")
        if (System.getenv("TRAVIS") == "true") {
            clientConfig.info.setBuildNumber("TRAVIS_"+System.getenv("TRAVIS_BUILD_NUMBER")+"_"+project.name)
            clientConfig.info.setBuildUrl("https://travis-ci.org/FilipMalczak/vent/builds/"+System.getenv("TRAVIS_BUILD_NUMBER"))
            clientConfig.info.setPrincipal("https://travis-ci.org/"+System.getenv("TRAVIS_REPO_SLUG"))
            clientConfig.info.setVcsRevision(System.getenv("TRAVIS_COMMIT"))
        }
    }

    task assertPublishingIsPossible {
        doFirst {
            assert publishingUser != null
            assert publishingKey != null
        }
    }

    task publishArtifacts(group: "publishing",
            description: "Published artifacts to Artifactory and, in case of master branch, to Bintray") {
        dependsOn assertPublishingIsPossible
        dependsOn artifactoryPublish
//        if (currentState == "RELEASE")
//            dependsOn bintrayUpload
    }

    //todo introduce nightly builds
    task publishArtifactsFromCI(group: "publishing",
            description: "If this is executed by Travis because of git push (and not pull request, etc), " +
                    "publish artifacts; else - no-op"){
        if (System.getenv("TRAVIS_EVENT_TYPE") == "push")
            dependsOn publishArtifacts
    }

    artifactoryPublish.shouldRunAfter(assertPublishingIsPossible)
//    bintrayUpload.shouldRunAfter(assertPublishingIsPossible)
}
//
//bintray {
//    user = project.publishingUser
//    key = project.publishingKey
//    publications = ['VentPublication']
//    pkg {
//        repo = bintrayRepo
//        name = 'vent'
//        licenses = ['MIT']
//        vcsUrl = 'https://github.com/FilipMalczak/vent.git'
//        githubRepo = "FilipMalczak/vent"
//        if (System.getenv("TRAVIS") == "true") {
//            attributes = [
//                buildNo: "TRAVIS_"+System.getenv("TRAVIS_BUILD_NUMBER"),
//                buildUrl: "https://travis-ci.org/FilipMalczak/vent/builds/"+System.getenv("TRAVIS_BUILD_NUMBER"),
//                ciUrl: "https://travis-ci.org/"+System.getenv("TRAVIS_REPO_SLUG"),
//                gitCommit: System.getenv("TRAVIS_COMMIT")
//            ]
//        }
//    }
//}


wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '4.7'
}

//todo migrate from travis.org to travis.com https://blog.travis-ci.com/2018-05-02-open-source-projects-on-travis-ci-com-with-github-apps?utm_source=Broadcast&utm_campaign=2may_release

